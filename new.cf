PDefs. Program ::= [Def];

terminator Def "";


comment "//";
comment "/*" "*/";
comment "#";

DFun. Def 		::= Fun;
DIFun. Def 		::= InFun;
DTypeDef. Def 	::= TypeDef;
DStructDef. Def ::= StructDef;
DUsing. Def ::= "using" QConst ";";
DDecVar. Def ::= DecVar ";";

InlineFunction. InFun ::= "inline" Fun;
FuctionHead. Fun ::= Type UIdent "(" [Arg] ")" FunBod;
FunctionBody. FunBod ::= ";";
FunctionBodyStm. FunBod ::= "{" [Stm] "}";


TypeDef. TypeDef ::= "typedef" Type UIdent ";";
StructDef. StructDef ::= "struct" UIdent "{" [DecVar] "}" ";";

ArgIdLess. Arg ::= Type;
ArgDecVar. Arg ::= Type UIdent;
ArgDecVarDefault. Arg ::= Type UIdent "=" Exp;
separator Arg ",";

DecVar. DecVar ::= Type UIdent;
DecVarDefault. DecVar ::= Type UIdent "=" Exp;
terminator DecVar ";";

TQConst. BType ::= QConst;
TInt. 	 BType ::= "int";
TDouble. BType ::= "double";
TBool.	 BType ::= "bool";
TVoid.	 BType ::= "void";
TType. Type ::= BType;
TConst. Type ::= "const" BType;
TConstRef. Type ::= "const" BType "&";
TRef. Type ::= BType "&";
separator nonempty Type ",";

VInt. Value ::= Integer;
VDouble. Value ::= Double;
VChar. Value ::= Char;
ValConcatString. Value ::= [ValString];

ValSingleString. ValString ::= String;
separator nonempty ValString "";


QConstRec. QConst ::= QConst "::" QConst;
QConstBase. QConst ::= UIdent;
QConstTemplateBase. QConst ::= TInstanciation; 
Template. TInstanciation ::= UIdent "<" [Type] ">";

StmDec. Stm ::= DecVar ";";
StmExp. Stm ::= Exp ";";
StmRet. Stm ::= "return" Exp ";";
StmWhile. Stm ::= "while" "(" Exp ")" Stm;
StmDo. Stm ::= "do" Stm "while" "(" Exp ")" ";";
StmFor. Stm ::= "for" "(" DecVar ";" Exp ";" Exp ")" Stm;
StmBlock. Stm ::= "{" [Stm] "}";
StmIf. Stm ::= "if" "(" Exp ")" Stm;
StmIfElse. Stm ::= "if" "(" Exp ")" Stm "else" Stm;

StmTypeDef. Stm ::= TypeDef;
StmStructDef. Stm ::= StructDef;
separator Stm "";

token UIdent (letter (letter | digit | '_')*) ;

ExValue.	Exp16 ::= Value;
ExIndex. 	Exp15 ::= Exp15 "[" Exp15 "]";
ExCall. 	Exp15 ::= Exp15 "(" [Exp] ")";
ExQConst. 	Exp15 ::= QConst;
ExProjPoint.Exp14 ::= Exp16 "." Exp15;
ExProjArrow.Exp14 ::= Exp16 "->" Exp15; 
ExPostInc. 	Exp14 ::= Exp15 "++";
ExPostDec. 	Exp14 ::= Exp15 "--";
ExDeref. 	Exp14 ::= "*" Exp15;
ExPreInc. 	Exp13 ::= "++" Exp14;
ExPreDec. 	Exp13 ::= "--" Exp14;
ExNot. 		Exp13 ::= "!" Exp14;
ExMult. 	Exp12 ::= Exp13 "*" Exp12;
ExDiv. 		Exp12 ::= Exp13 "/" Exp12;
ExMod. 		Exp12 ::= Exp13 "%" Exp12;
ExAdd. 		Exp11 ::= Exp12 "+" Exp11;
ExSub. 		Exp11 ::= Exp12 "-" Exp11;
ExLeftShft.	Exp10 ::= Exp11 "<<" Exp10;
ExRightSft.	Exp10 ::= Exp11 ">>" Exp10;
ExLess. 	Exp9 ::= Exp10 "<" Exp9;
ExGreater. 	Exp9 ::= Exp10 ">" Exp9;
ExLessEq. 	Exp9 ::= Exp10 "<=" Exp9;
ExGreaterEq. Exp9 ::= Exp10 ">=" Exp9;
ExEqual.	Exp8 ::= Exp9 "==" Exp8;
ExInequal.	Exp8 ::= Exp8 "!=" Exp7;
ExConjunc.	Exp4 ::= Exp5 "&&" Exp4;
ExDisjunc.	Exp3 ::= Exp4 "||" Exp3;
ExAssign.	Exp2 ::= Exp3 "=" Exp2;
ExAddAssign. Exp2 ::= Exp3 "+=" Exp2;
ExSubAssign. Exp2 ::= Exp3 "-=" Exp2;
ExCond.		Exp2 ::= Exp2 "?" Exp2 ":" Exp2;
ExThrow.	Exp1 ::= "throw" Exp;
coercions Exp 16;
separator Exp ",";